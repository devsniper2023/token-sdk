buildscript {
    ext.kotlin_version = '1.0.3'
    ext.corda_version = '0.4-SNAPSHOT'
    ext.quasar_version = '0.7.6'
    ext.jersey_version = '2.23.1'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.r3corda:quasar:$corda_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'com.r3corda.quasar'
apply plugin: Cordformation

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // Corda integration dependencies
    compile "com.r3corda:core:$corda_version"
    compile "com.r3corda:contracts:$corda_version"
    compile "com.r3corda:node:$corda_version"
    compile "com.r3corda:corda:$corda_version"

    // Javax is required for webapis
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"

    // TODO: Would like to specify dependent cordsapps like this
    //cordapp "vega"

    // Cordapp dependencies
    // Specify your cordapp's dependencies below
}

task deployNodes(type: Cordform, dependsOn: 'build') {
    directory "./build/testnodes"
    networkMap "NodeA"
    node {
        name "Notary"
        dirName "notary"
        nearestCity "London"
        notary true
        advertisedServices = []
        artemisPort 31337
    }
    node {
        name "NodeA"
        dirName "notary"
        nearestCity "London"
        advertisedServices = []
        artemisPort 31338
        webPort 31339
    }
    node {
        name "NodeB"
        dirName "notary"
        nearestCity "New York"
        advertisedServices = []
        artemisPort 31340
        webPort 31341
    }
}

// TODO: Create cloudformation style template that this generates nodes from
task deployNodesOld(dependsOn: 'build') << {
    def cordaJar = configurations.runtime.filter { it.toString().contains("corda-${corda_version}.jar")}.getSingleFile()
    def appDeps = configurations.runtime.filter { it != cordaJar } // TODO: Filter out all other deps in the corda jar
    copy {
        from cordaJar
        from 'config/dev/nameservernode.conf'
        into "${buildDir}/nodes/nameserver"
        rename 'nameservernode.conf', 'node.conf'
    }

    copy {
        from appDeps
        from jar
        into "${buildDir}/nodes/nameserver/plugins"
    }

    copy {
        from cordaJar
        from 'config/dev/generalnodea.conf'
        into "${buildDir}/nodes/nodea"
        rename 'generalnodea.conf', 'node.conf'
    }

    copy {
        from appDeps
        from jar
        into "${buildDir}/nodes/nodea/plugins"
    }

    copy {
        from cordaJar
        from 'config/dev/generalnodeb.conf'
        into "${buildDir}/nodes/nodeb"
        rename 'generalnodeb.conf', 'node.conf'
    }

    copy {
        from appDeps
        from jar
        into "${buildDir}/nodes/nodeb/plugins"
    }

    delete("${buildDir}/nodes/runnodes")
    def jarName = cordaJar.getName()
    copy {
        from "buildSrc/scripts/runnodes"
        filter { String line -> line.replace("JAR_NAME", jarName) }
        filter(org.apache.tools.ant.filters.FixCrLfFilter.class, eol: org.apache.tools.ant.filters.FixCrLfFilter.CrLf.newInstance("lf"))
        into "${buildDir}/nodes"
    }
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

quasarScan.dependsOn('classes')
buildscript {
    ext.kotlin_version = '1.0.3'
    ext.corda_version = '0.4-SNAPSHOT'
    ext.quasar_version = '0.7.6'
    ext.jersey_version = '2.23.1'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.r3corda.plugins:quasar-utils:$corda_version"
        classpath "com.r3corda.plugins:publish-utils:$corda_version"
        classpath "com.r3corda.plugins:cordformation:$corda_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'com.r3corda.plugins.quasar-utils'
apply plugin: 'com.r3corda.plugins.publish-utils'
apply plugin: 'com.r3corda.plugins.cordformation'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // Corda integration dependencies
    compile "com.r3corda:core:$corda_version"
    compile "com.r3corda:contracts:$corda_version"
    compile "com.r3corda:node:$corda_version"
    compile "com.r3corda:corda:$corda_version"
    compile "com.r3corda:test-utils:$corda_version"

    // Javax is required for webapis
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"

    // Cordapp dependencies
    // Specify your cordapp's dependencies below, including dependent cordapps
}

task deployNodes(type: com.r3corda.plugins.Cordform, dependsOn: ['build']) {
    directory "./build/nodes"
    networkMap "Controller"
    node {
        name "Controller"
        dirName "controller"
        nearestCity "London"
        advertisedServices ["corda.notary.validating"]
        artemisPort 12345
        webPort 12346
        cordapps []
    }
    node {
        name "NodeA"
        dirName "nodea"
        nearestCity "London"
        advertisedServices []
        artemisPort 31337
        webPort 31339
        cordapps []
    }
    node {
        name "NodeB"
        dirName "nodeb"
        nearestCity "New York"
        advertisedServices []
        artemisPort 31338
        webPort 31340
        cordapps []
    }
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

quasarScan.dependsOn('classes')

publishing {
    publications {
        jarAndSources(MavenPublication) {
            from components.java
            artifactId 'template-cordapp'

            artifact sourceJar
            artifact javadocJar
        }
    }
}